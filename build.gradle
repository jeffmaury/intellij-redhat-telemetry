plugins {
    id "org.jetbrains.intellij" version "${intellijPluginVersion}"
    id "idea"
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

intellij {
    version = ideaVersion //for a full list of Jetbrains IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'Telemetry by Red Hat'
    updateSinceUntilBuild = false
}

patchPluginXml {
    sinceBuild = sinceIdeaBuild
    version = projectVersion
}

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token = jetBrainsToken
    channels = [jetBrainsChannel]
}

dependencies {
    implementation('com.segment.analytics.java:analytics:3.4.0') {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'com.squareup.retrofit2', module: 'retrofit-mock'
        exclude group: 'com.google.auto.value', module: 'auto-value-annotations'
    }
    implementation('com.squareup.okio:okio:3.6.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation(
            'com.google.code.gson:gson:2.10.1'
    )
    testImplementation(
            "org.junit.jupiter:junit-jupiter:5.9.1",
            "org.junit.platform:junit-platform-launcher:1.9.1",
            "org.assertj:assertj-core:3.24.2",
            "org.mockito:mockito-inline:4.5.1"
    )
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }
}

java {
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
}

group 'com.redhat.devtools.intellij'
version projectVersion
//buildSearchableOptions.enabled = false
